apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def alias = properties.get('alias')
def _keyPassword = properties.get('keyPassword')
def _storePassword = properties.get('storePassword')
def keystorePath = properties.get('keystorePath')

def CHANNEL = System.getProperty("channel", "default")

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION as String
    defaultConfig {
        applicationId "com.kinglloy.album"
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode APP_VERSION_CODE as int
        versionName APP_VERSION_NAME as String

        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'x86', 'armeabi', 'armeabi-v7a'
        }
    }
    signingConfigs {
        release {
            keyAlias alias
            keyPassword _keyPassword
            storeFile file(keystorePath)
            storePassword _storePassword
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'component-proguard.pro'

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"
    productFlavors {
        demo {
            dimension "default"
            buildConfigField("boolean", "DEMO_MODE", "true")
        }
        production {
            dimension "default"
            buildConfigField("boolean", "DEMO_MODE", "false")
        }
        ultimate {
            dimension "default"
            applicationId "com.kinglloy.album.ultimate"
            buildConfigField("boolean", "DEMO_MODE", "false")
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation("com.mikepenz:materialdrawer:6.0.0@aar") {
        transitive = true
    }

    implementation "com.google.code.gson:gson:${GSON_VERSION}"
    implementation "com.squareup.okhttp3:okhttp:${OK_HTTP_VERSION}"
    implementation "com.google.dagger:dagger:${DAGGER_VERSION}"
    implementation "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
    implementation "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"
    implementation "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${KOTLIN_VERSION}"
    implementation "org.jetbrains.anko:anko-common:${ANKO_VESION}"

    implementation "com.google.android.gms:play-services-base:${FIRE_BASE_VERSION}"
    implementation "com.google.android.gms:play-services-wallet:${FIRE_BASE_VERSION}"
    implementation "com.google.firebase:firebase-core:${FIRE_BASE_VERSION}"
    implementation "com.google.firebase:firebase-crash:${FIRE_BASE_VERSION}"
    implementation "com.google.firebase:firebase-ads:${FIRE_BASE_VERSION}"

    implementation 'com.afollestad.material-dialogs:core:0.9.4.7@aar'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.1@aar'

    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    implementation 'com.android.support:multidex:1.0.2'

//    demoImplementation 'com.facebook.stetho:stetho:1.5.0'

    implementation project(':engine')
    implementation project(':data')

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def flavor = variant.productFlavors[0].name

        outputFileName = "${flavor}-LWA-${CHANNEL}-${APP_VERSION_NAME}.apk"
    }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${KOTLIN_VERSION}"
    }
}
apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}